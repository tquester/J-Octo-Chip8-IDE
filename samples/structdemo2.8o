            jump main
# The goal is to separate functions in a way that they work by themselfs, not even touchting the registers of the caller
# we can do it with save and load however without the help of structs the code may become difficult to read.
# Other than high level languages, the struct only stores register numbers, so we can rename 15 registers with a struct
# writing something like Coordinates.x instead of v1
#
# The struct itself compiles into the last register, so if we write save Coordinates this will compile into the number of registers
# needed to store.
#
# Coordinates.byte will create as many bytes, as it needs to store the structure. If we modify the members in the struct,
# data and save/load commands will automatically change
#
# So what we get with struct is:
#   - a local alias for registers
#   - automatic store/load

# Store the data of our enemy. We can move as many enemies as we like. Each one has its own data store in memory
# a struct mapps names to registers and simplyfies saving and loading to memory
# the struct name itself returns the last register, "save Enemy" compiles to "save vc"
# the members compile to a register name. "Enemy.x" compiles to v0
# .byte compiles into the number of bytes needed in memory, so Enemy.byte compiles to 13 zeros

:struct Enemy {
              x                                                       # v0 x coordinate
              y                                                       # v1 y coordinate
              dx                                                      # v2 direction x
              dy                                                      # v3 direction y
              minx                                                    # v4 min x
              maxx                                                    # v5 max x
              miny                                                    # v6 min y
              maxy                                                    # v7 max y
              frame                                                   # v8 sprite frame
              maxframe                                                # v9 sprite max frame
              dframe                                                  # va sprite frame direction
              drawx                                                   # vb The draw function uses this as x
              drawy                                                   # vc The draw function uses this as y
              drawframe                                               # vd frame for drawing
            }


# Enemy.dx := - Enemy.dx
: enemyReverseX
            vf := 0
            Enemy.dx =- vf
            return

# Enemy.dy := - Enemy.dy
: enemyReverseY
            vf := 0
            Enemy.dy =- vf
            return
# Enemy.dframe := - Enemy.dframe
: enemyReverseFrame
            vf := 0
            Enemy.dframe =- vf
            return

# Advance the coordinates of your enemy, if we reach the bounds, revers the direction
# Advance the frame count, if we reach the lower or upper bound, reverse the direction.

: calcEnemyCoords

            Enemy.drawx := Enemy.x                                    # save the old coordinates
            Enemy.drawy := Enemy.y                                    # and the old frame number
            Enemy.drawframe := Enemy.frame                            # for deletion draw

            if Enemy.dx != 0 begin                                    # if enemy moves x
              Enemy.x += Enemy.dx                                     # modify coordinate
              if Enemy.x <= Enemy.minx then enemyReverseX             # reverse at start
              if Enemy.x >= Enemy.maxx then enemyReverseX             # and end
            end
            if Enemy.dy != 0 begin                                    # if it moves in y
              Enemy.y += Enemy.dy                                     # advance and
              if Enemy.y <= Enemy.miny then enemyReverseY             # reverse at start
              if Enemy.y >= Enemy.maxy then enemyReverseY             # and end
            end
            Enemy.frame += Enemy.dframe                               # go to next/prev frame
            if Enemy.frame < 0 then enemyReverseFrame                 # if it was the first frame count forward
            if Enemy.frame >= Enemy.maxframe begin
              enemyReverseFrame
              Enemy.frame += Enemy.dframe
            end
# if it was the last frame, count backwards
            return

# Since there is only one index register and this can not be transfered to regular registers,
# we only can write it as code constant

:macro moveenemy adr drawfn {
              i := adr                                                # load enemy data
              load Enemy
              calcEnemyCoords                                         # move by coordinates
              drawfn                                                  # delete-draw the enemy
              copyEnemyCoords
              drawfn
              i := adr                                                # save data
              save Enemy
            }

: copyEnemyCoords
            Enemy.drawx := Enemy.x                                    # draw the enemy
            Enemy.drawy := Enemy.y                                    # at new
            Enemy.drawframe := Enemy.frame                            # coordinates
            return


:macro drawInitEnemy adr drawfn {
              i := adr                                                # load enemy
              load Enemy
              Enemy.drawx := Enemy.x                                  # draw it at x,y
              Enemy.drawy := Enemy.y
              Enemy.drawframe := Enemy.frame
              drawfn
            }

# ---------------------------------------------------------------------------
# -- Game starts here
# ---------------------------------------------------------------------------

: main
            hires


# copy 4 the initial coordinate data for all 4 enemies to the data section
            ve := 0
            for v0 := 0 to 4 begin
              i := temp
              save v0                                                 # we ran out of register, save the for variable
              i := initEnemyData                                      # load 14 registers
              i += ve
              load Enemy
              i := enemydata1                                         # save them
              i += ve
              save Enemy
              ve += initEnemyData1 - initEnemyData                    # next offset
              i := temp
              load v0
            end

# draw all 4 enemies at the start position. The next draw will remove and replace them

            drawInitEnemy enemydata1 drawPackman                      # draw initital
            drawInitEnemy enemydata2 drawPackman                      # draw initial
            drawInitEnemy enemydata3 drawBird                         # draw initial
            drawInitEnemy enemydata4 drawHeliBird                     # draw initial

# loop endless
            loop                                                      # forwever
              i := savedata                                           # save registers
              save Enemy
              moveenemy enemydata1 drawPackman                        # move enemy1
              moveenemy enemydata2 drawPackman                        # move enemy2
              moveenemy enemydata3 drawBird                           # move enemy2
              moveenemy enemydata4 drawHeliBird                       # move enemy2
              i := savedata                                           # load registers
              load Enemy

# here we can place our game code. All registers will be intact
              for v0 := 0 to 50 v0 += 0                               # wait some time

            again

# because there is only one I register and we can not save it, each sprite needs a draw routine. this will be passed into the draw macro

: drawPackman
            i := pacman                                               # out sprite
            i += Enemy.drawframe                                      # add frame to sprite
            sprite Enemy.drawx Enemy.drawy 8                          # draw
            return
: drawBird
            i := bird                                                 # out sprite
            i += Enemy.drawframe                                      # add frame to sprite
            sprite Enemy.drawx Enemy.drawy 8                          # draw
            return
: drawHeliBird
            i := helibird                                             # out sprite
            i += Enemy.drawframe                                      # add frame to sprite
            sprite Enemy.drawx Enemy.drawy 8                          # draw
            return


# ---------------------------------------------------------------------
# -- Initial data for the coordinates and movements of all enemies  ---
# ---------------------------------------------------------------------
: initEnemyData
# packman 1
            Enemy.byte {
              x := 20
              y := 5
              dx := 1
              dy := 0
              minx := 10
              maxx := 100
              miny := 5
              maxy := 5
              dframe := 8
              maxframe := 24
            }
: initEnemyData1
# packman 2
            Enemy.byte {
              x := 5
              y := 12
              dx := 0
              dy := 1
              minx := 5
              maxx := 5
              miny := 5
              maxy := 50
              dframe := 8
              maxframe := 24
            }
# the bird
            Enemy.byte {
              x := 20
              y := 50
              dx := 2
              dy := 1
              minx := 5
              maxx := 120
              miny := 5
              maxy := 55
              dframe := 8
              maxframe := 32
            }
# the bird copter
            Enemy.byte {
              x := 10
              y := 30
              dx := 1
              dy := 2
              minx := 5
              maxx := 120
              miny := 5
              maxy := 55
              dframe := 8
              maxframe := 32
            }


: enemydata1 Enemy.byte
: enemydata2 Enemy.byte
: enemydata3 Enemy.byte
: enemydata4 Enemy.byte
: savedata Enemy.byte
: temp
            0x00



: pacman
# Sprite 1
            0x38 0x7c 0xee 0xfe 0xfe 0x7c 0x38 0x00
# Sprite 3
            0x38 0x7c 0xee 0xfe 0xe0 0x7c 0x38 0x00
# Sprite 4
            0x38 0x7c 0xee 0xfe 0xc0 0x64 0x38 0x00
# Sprite 5
            0x38 0x7c 0xee 0xfe 0xc0 0x64 0x38 0x00
: bird
# Sprite 1
            0x00 0x00 0x02 0xff 0x02 0x00 0x00 0x00
# Sprite 2
            0x00 0x00 0x32 0xff 0x32 0x00 0x00 0x00
# Sprite 3
            0x00 0x30 0x7a 0xff 0x7a 0x30 0x00 0x00
# Sprite 4
            0x30 0x78 0xfa 0xff 0xfa 0x70 0x30 0x00

: helibird
# Sprite 1
            0x00 0x10 0x10 0x7d 0x7f 0x3d 0x3c 0x24
# Sprite 2
            0x00 0x38 0x10 0x7c 0x7f 0x3c 0x3c 0x24
# Sprite 3
            0x00 0x7c 0x10 0x7d 0x7f 0x3d 0x3c 0x24
# Sprite 4
            0x00 0xfe 0x10 0x7c 0x7f 0x3c 0x3c 0x24
# Sprite 5
            0x00 0xfe 0x10 0x7c 0x7f 0x3c 0x3c 0x24
